apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'signing'

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    google()
    mavenCentral()
}

kapt {
    mapDiagnosticLocations = true
    useBuildCache = true
}

android {
    compileSdk androidCompileSdkVersion
    testOptions.unitTests.includeAndroidResources = true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.motorro.appupdatewrapper'

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    api 'androidx.core:core-ktx:1.13.1'
    api 'androidx.lifecycle:lifecycle-common:2.8.4'
    api 'androidx.activity:activity-ktx:1.9.1'
    api 'com.google.android.play:app-update:2.1.0'
    api 'com.google.android.play:app-update-ktx:2.1.0'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation project(":testapp")
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'androidx.test.ext:junit:1.2.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.robolectric:robolectric:4.13'
    testImplementation 'androidx.lifecycle:lifecycle-runtime-testing:2.8.4'
}

dokkaJavadoc.configure {
    outputDirectory.set(new File("$buildDir/javadoc"))
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

def libId = 'appupdatewrapper'
def libName = 'appupdatewrapper'
def libDesc = 'A wrapper for Android `AppUpdateManager` to encapsulate update workflow.'

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = rootProject.group
                artifactId = libId
                version = rootProject.version

                pom {
                    name = libName
                    description = libDesc
                    url = projectUrl
                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = projectScm
                        developerConnection = projectScm
                        url = projectUrl
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}
